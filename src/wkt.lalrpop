// http://svn.osgeo.org/postgis/trunk/doc/bnf-wkt.txt

use std::str::FromStr;
use ::New;

grammar;

//FIXME: https://github.com/nikomatsakis/lalrpop/issues/97
//Float: f64 = r"[+-]?([0-9]+(.[0-9]*)?|.[0-9]+)([eE][+-]?[0-9]+)?" => f64::from_str(<>).unwrap();

Float: f64 = r"[+-0-9eE.]+" => f64::from_str(<>).unwrap();

Comma<T>: Vec<T> = {
    <h:(<T> ",")*> <t:T?> =>
        h.into_iter().chain(t).collect()
};

pub WellKnownTextRepresentation: New = {
    PointTextRepresentation => New::Point(<>),
    CurveTextRepresentation => New::Curve(<>),
    SurfaceTextRepresentation => New::Surface(<>),
    CollectionTextRepresentation,
};

PointTextRepresentation = "POINT" <PointText>;

CurveTextRepresentation = {
    LineStringTextRepresentation,
};

LineStringTextRepresentation = "LINESTRING" <LineStringTextBody>;

SurfaceTextRepresentation = CurvePolygonTextRepresentation;

CurvePolygonTextRepresentation = {
    PolygonTextRepresentation,
};

PolygonTextRepresentation = "POLYGON" <PolygonTextBody>;

CollectionTextRepresentation: New = {
    MultiPointTextRepresentation => New::Curve(<>),
    GeometryCollectionTextRepresentation => New::Collection(<>),
};

MultiPointTextRepresentation = "MULTIPOINT" <MultiPointText>;

GeometryCollectionTextRepresentation =
    "GEOMETRYCOLLECTION" <GeometryCollectionText>;

LineStringTextBody = LineStringText;

PolygonTextBody = PolygonText;

PointText: Option<(f64, f64)> = {
    "EMPTY" => None,
    "(" <n:Point> ")" => Some(n),
};

Point: (f64, f64) = <x:Float> <y:Float> => (x, y);

LineStringText: Vec<(f64, f64)> = {
    "EMPTY" => vec![],
    "(" <n:Comma<Point>> ")" => n,
};

PolygonText: Vec<Vec<(f64, f64)>> = {
    "EMPTY" => vec![],
    "(" <n:Comma<LineStringText>> ")" => n,
};

MultiPointText: Vec<(f64, f64)> = {
    "EMPTY" => vec![],
    "(" <n:Comma<Point>> ")" => n,
};

GeometryCollectionText: Vec<New> = {
    "EMPTY" => vec![],
    "(" <n:Comma<WellKnownTextRepresentation>> ")" => n,
};
